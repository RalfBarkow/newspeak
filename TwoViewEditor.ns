Newspeak3
'Root'
class TwoViewEditor usingPlatform: p  = (
(*
This module implements an editor with two views: one view is a text editor on raw HTML, and the second is the same HTML, rendered. The editor is live - every edit causes immediate change in the rendered HTML.

Importantly, the HTML can specify embedded Hopscotch fragments using Ampleforth (still unsupported).  Indeed, the primary purpose here is to facilitate the creation of live documents which include programmed UI elements such as live literate programs. 

Design discussion:

The question arises, how to specify the mapping from class names used in the HTML to actual fragments. This mapping is required by Ampleforth. The set of UI elements one might want to embed is open-ended, and often created by the document author. Therefore we need to able to compute a fragment instance in the context of the document. This leads to the need to access all standard fragment classes, and to able to load new ones. Better yet, it would be nice to support widget development within the tool.

The above leads to the view that  this tool is an extension of the IDE (or even, part of the IDE). This aspect is still unimplemented. Need to figure out how to provide it in the UI. We will perhaps want a workspace that has access to this editor (or conversely, a presenter on the editor that has access to the IDE namespace) so that we can define the mapping that will be used by Ampleforth. And we'd want access to the IDE namespace, and so we end up with what  amounts to providing the IDE as part of the tool. It then seems natural to make this part of the IDE, so the namespace access, as well as search, help and workspaces are part of the UI already. Then the mapping is a slot of this module and accessible from regular workspaces. We have one mapping for all documents. We could also have per subject mappings, and set them on subject creation, managing them as sub-namespaces in the IDE.
*)
	|	
    private Subject = p hopscotch Subject.
    private Presenter = p hopscotch Presenter.
	|
) (
public class TwoViewEditorSubject onModel: m <Array[String]> = Subject onModel: m (
(*  We really want a mutable string as a model, but there is no such
thing in our system, so we use a one-element Array[String].
*)
  | public isAmpleforth <Boolean> ::= false. |
) (
public createPresenter ^ <Presenter>  = (
  ^TwoViewEditorPresenter onSubject: self
)
public text ^ <String> = (
  ^model first
)
public setText: s <String> = (
  model at: 1 put: s
)
public isKindOfTwoViewSubject ^ <Boolean> = (
  ^true
)
isMyKind: f <Fragment> ^ <Boolean> = (
  ^f isKindOfTwoViewSubject
)
) : (
public initialContents: s <String> = (
  ^onModel: (Array withAll: {s})
)
)
class TwoViewEditorPresenter onSubject: s <TwoViewEditorSubject> = Presenter onSubject: s (
  | rendered <HolderComposer> = holder: (render: subject text). |
) (
public isKindOfTwoViewPresenter ^ <Boolean> = (
  ^true
)
isMyKind: f <Fragment> ^ <Boolean> = (
  ^f isKindOfTwoViewPresenter
)
definition ^ <Fragment> = (
  | cm <CodeMirrorFragment> = codeMirror: subject text. | 
  rendered content: (render: subject text). 
  cm 
    changeResponse: [:ed <CodeMirrorFragment> :event <Event>  | 
    subject setText: ed textBeingAccepted. 
    rendered content: (render: subject text) 
    ]; 
    (*cancelResponse: cancelResponse;*)
    acceptResponse: [:ed <CodeMirrorFragment> :event <Event>  | updateGUI: [ subject setText: ed textBeingAccepted]].
  ^row: { expanded: [cm] collapsed: [nothing]. rendered}
)
updateVisualsFromSameKind: oldFragment <Fragment> ^ <Alien[Element]> = (
  ^visual
)
render: s = (
(*  Problem: there is no method #ampleforth:. The actual method is #ampleforth:mapping:.
The root issue here is that we have no mapping. specified. We need real thought over how to define/provide a mapping
in the editor, and how to provide access to the various widgets one might want to use.
*)
  ^subject isAmpleforth ifTrue: [ampleforth: s] ifFalse: [html: s]
)
) : (
)
) : (
)
