Newspeak3
'Root'
class JSTesting usingPlatform: platform testFramework: minitest = (
|
	private js = platform js.
	private TestContext = minitest TestContext.
|) (
public class JSTests = TestContext (
) (
public testJSEval = (
	assert: (js global eval: 'null') equals: nil.
	assert: (js global eval: 'false') equals: false.
	assert: (js global eval: 'true') equals: true.
	assert: (js global eval: '42') equals: 42.
	assert: (js global eval: '42.0') equals: 42.0 asFloat.
	assert: (js global eval: '"string"') equals: 'string'.
)
public testJSMath = (
	| Math n |
	Math:: js global at: 'Math'.

	n:: Math random.
	assert: n isKindOfFloat.
	assert: n >= 0.0 asFloat.
	assert: n < 1.0 asFloat.

	n:: Math at: 'E'.
	assert: n isKindOfFloat.
	assert: n > 2.718 asFloat.
	assert: n < 2.719 asFloat.
)
public testJSObject = (
	| JSObject object |
	JSObject:: js global at: 'Object'.
	object:: JSObject new.

	should: [object methodDoesNotExist] signal: Exception.

	assert: (object at: 'property' put: 42) equals: 42.
	assert: (object at: 'property') equals: 42.
	assert: (object removeKey: 'property') equals: true.
)
public testJSString = (
	(* String argument *)
	assert: (js global parseInt: '42') equals: 42.

	(* String return value *)
	assert: ((js global at: 'Math') toString) equals: '[object Math]'.
)
public testJSUndefined = (
	assert: js undefined isUndefined.
	deny: js global isUndefined.
	assert: (js global at: 'DoesNotExist') isUndefined.
	deny: (js global at: 'Object') isUndefined.
	assert: js undefined printString equals: 'undefined'.
)
) : (
TEST_CONTEXT = ()
)
) : (
)
